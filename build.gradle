plugins {
    id 'java'
    id 'jacoco'

    // https://plugins.gradle.org/plugin/org.openjfx.javafxplugin
    id 'org.openjfx.javafxplugin' version '0.0.13'
    // https://plugins.gradle.org/plugin/org.fulib.fulibGradle
    id 'org.fulib.fulibGradle' version '0.5.0'
}

group 'de.uniks.beastopia.teaml'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/fr.brouillard.oss/cssfx
    implementation group: 'fr.brouillard.oss', name: 'cssfx', version: '11.5.1'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.2'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.2'

    // https://mvnrepository.com/artifact/org.testfx/testfx-junit5
    testImplementation group: 'org.testfx', name: 'testfx-junit5', version: '4.0.16-alpha'

    // https://mvnrepository.com/artifact/org.testfx/openjfx-monocle
    testImplementation group: 'org.testfx', name: 'openjfx-monocle', version: 'jdk-12.0.1+2'

    // https://mvnrepository.com/artifact/org.fulib/fulib
    fulibScenarios group: 'org.fulib', name: 'fulib', version: '1.6.1'

    // https://mvnrepository.com/artifact/org.fulib/fulibScenarios
    fulibScenarios group: 'org.fulib', name: 'fulibScenarios', version: '1.7.0'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    fulibScenarios group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'

    // https://mvnrepository.com/artifact/org.fulib/fulibTools
    testImplementation group: 'org.fulib', name: 'fulibTools', version: '1.5.1'

    // https://mvnrepository.com/artifact/org.fulib/fulibTables
    testImplementation group: 'org.fulib', name: 'fulibTables', version: '1.4.0'

    // https://mvnrepository.com/artifact/org.fulib/fulibMockups
    testImplementation group: 'org.fulib', name: 'fulibMockups', version: '0.4.0'

    // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.2.0'

    // https://mvnrepository.com/artifact/org.fulib/fulibYaml
    implementation group: 'org.fulib', name: 'fulibYaml', version: '1.5.0'

    // https://mvnrepository.com/artifact/com.squareup.retrofit2/retrofit
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.9.0'

    // https://mvnrepository.com/artifact/com.squareup.retrofit2/converter-jackson
    implementation group: 'com.squareup.retrofit2', name: 'converter-jackson', version: '2.9.0'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.3'

    // https://mvnrepository.com/artifact/io.reactivex.rxjava3/rxjava
    implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.1.4'

    // https://mvnrepository.com/artifact/com.squareup.retrofit2/adapter-rxjava3
    implementation group: 'com.squareup.retrofit2', name: 'adapter-rxjava3', version: '2.9.0'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'

    // https://mvnrepository.com/artifact/com.google.dagger/dagger
    implementation group: 'com.google.dagger', name: 'dagger', version: '2.42'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.42'
    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.42'

    // https://mvnrepository.com/artifact/javax.websocket/javax.websocket-api
    compileOnly group: 'javax.websocket', name: 'javax.websocket-api', version: '1.0'

    // https://mvnrepository.com/artifact/javax.json/javax.json-api
    compileOnly group: 'javax.json', name: 'javax.json-api', version: '1.1.4'

    // https://mvnrepository.com/artifact/org.glassfish/javax.json
    implementation group: 'org.glassfish', name: 'javax.json', version: '1.1.4'

    // https://mvnrepository.com/artifact/org.glassfish.tyrus/tyrus-client
    implementation group: 'org.glassfish.tyrus', name: 'tyrus-client', version: '1.20'

    // https://mvnrepository.com/artifact/org.glassfish.tyrus/tyrus-container-grizzly-client
    implementation group: 'org.glassfish.tyrus', name: 'tyrus-container-grizzly-client', version: '1.20'

    // https://mvnrepository.com/artifact/org.openjfx/javafx-media
    implementation group: 'org.openjfx', name: 'javafx-media', version: '21-ea+21'
}

javafx {
    version = '20'
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml', 'javafx.media']
}

jacoco {
    toolVersion = '0.8.9'
}

jacocoTestReport {
    reports.xml.required = true
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
        showStackTraces = true
    }

    if (hasProperty('headless') || System.getenv('CI')) {
        systemProperties = [
                'java.awt.headless': 'true',
                'testfx.robot'     : 'glass',
                'testfx.headless'  : 'true',
                'glass.platform'   : 'Monocle',
                'monocle.platform' : 'Headless',
                'prism.order'      : 'sw',
                'prism.text'       : 't2k',
        ]
    }
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('fatJar', Jar) {
    manifest {
        attributes 'Main-Class': "de.uniks.beastopia.teaml.Main"
    }
    archiveBaseName = "${rootProject.name}"
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}